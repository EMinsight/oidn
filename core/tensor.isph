// Copyright 2009-2021 Intel Corporation
// SPDX-License-Identifier: Apache-2.0

#pragma once

#include "vec.isph"

#define K programCount // native tensor block size

// Tensor in CHW layout
struct TensorAccessor
{
  uniform float* uniform ptr;
  uniform int C;
  uniform int H;
  uniform int W;
};

inline size_t getIndex(uniform TensorAccessor& tz, uniform int c, uniform int h, int w)
{
#if defined(OIDN_DNNL)
  // ChwKc layout (blocked)
  return ((size_t)tz.H * (c/K) + h) * ((size_t)tz.W*K) + (size_t)w*K + (c%K);
#else
  // chw layout
  return ((size_t)tz.H * c + h) * (size_t)tz.W + w;
#endif
}

inline float get1f(uniform TensorAccessor& tz, uniform int c, uniform int h, int w)
{
  return tz.ptr[getIndex(tz, c, h, w)];
}

inline void set1f(uniform TensorAccessor& tz, uniform int c, uniform int h, int w, float value)
{
  tz.ptr[getIndex(tz, c, h, w)] = value;
}

inline vec3f get3f(uniform TensorAccessor& tz, uniform int c, uniform int h, int w)
{
  return make_vec3f(get1f(tz, c,   h, w),
                    get1f(tz, c+1, h, w),
                    get1f(tz, c+2, h, w));
}

inline void set3f(uniform TensorAccessor& tz, uniform int c, uniform int h, int w, const vec3f& value)
{
  set1f(tz, c,   h, w, value.x);
  set1f(tz, c+1, h, w, value.y);
  set1f(tz, c+2, h, w, value.z);
}
